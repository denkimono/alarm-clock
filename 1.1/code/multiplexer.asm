
; MULTIPLEXER INTERRUPT (100Hz)
MPX_INTERRUPT

	; DECREMENT 10ms TICK COUNTER
	; (NOT STRICTLY PART OF THE MULTIPLEXER!)
	DECF	SUBTICKS,F
	BTFSC	STATUS,Z
	CALL	ITICKS ; ITICKS IS IN INTERRUPTS.ASM
	;


	; CONTROL DISPLAY BRIGHTNESS
	INCF	FRAME_COUNT,F

	; CLEAR BRIGHTNESS PWM COUNTER
	MOVLW	0X09
	SUBWF	FRAME_COUNT,W
	BTFSC	STATUS,Z
	CLRF	FRAME_COUNT


	; CHECK FOR LOW POWER MODE
	CASE	LP_MODE,1
	GOTO	LOW_POWER_MODE

	; EXTINGUISH DISPLAY
	MOVLF	PORTC,0X7F
	CLRF	PORTA

	; SELECT NEXT DIGIT
	INCF	MPX_DIGIT,F


; SELECT BIT PATTERN FROM DISPLAY MEMORY
	MOVFW	MPX_DIGIT
	ADDLW	DIG1
	MOVWF	FSR
	MOVFW	INDF
	CALL	PTRN	; get bit pattern
	MOVWF	PORTC	; update segment drive
;

; ENABLE CURRENT DIGIT
	MOVLW	0X06
	SUBWF	MPX_DIGIT,W

	BTFSS	STATUS,Z
	GOTO	MPX_I1

; SELECT DIGIT 7 (PORT C)
	BSF	PORTC,DIGIT7
; MAKE NEXT INCREMENT EQUAL ZERO	
	MOVLF	MPX_DIGIT,0XFF

	GOTO	MPX_I2

; SELECT DIGITS 1-6 (PORT A)
MPX_I1

	IF SYSTEM_VERSION == PROTOTYPE_1

		; ROTATE 1 RIGHT BY MPX_DIGIT
		MOVLF	ITEMP1,0X01
		MOVFW	MPX_DIGIT
		MOVWF	ICOUNTER
		BTFSC	STATUS,Z
		GOTO	MPX_I5
MPX_I4
		RLF	ITEMP1,F

		DECFSZ	ICOUNTER,F
		GOTO	MPX_I4

MPX_I5

		MOVFW	ITEMP1

	ELSE

		; LOOK UP PORTA DIGIT SELECT VALUE FROM TABLE
		MOVFW	MPX_DIGIT
		CALL	DIGIT_LOOKUP

	ENDIF

	MOVWF	PORTA

MPX_I2

	; TURN THE DISPLAY OFF DEPENDING ON THE BRIGHTNESS
	; CHECK VALUE IS < 10
	MOVFW	BRIGHTNESS
	SUBWF	FRAME_COUNT,W
	BTFSC	STATUS,C
	GOTO	MPX_I6
	;FRAME_COUNT >= BRIGHTNESS

	; EXTINGUISH DISPLAY
	MOVLF PORTC,0X7F
	CLRF PORTA

MPX_I6
	BCF		INTCON,T0IF
	RETURN

LOW_POWER_MODE
	
;FLASH A COLON LED EVERY 3 SECONDS	

; on strt D_DIV=divisor, D_NUM=numerator
	MOVLF	D_DIV,D'3'
	MOVFW	C_SEC
	MOVWF	D_NUM
	CALL	DIV

; on exit D_MOD=modulus, D_NUM=remainder
	MOVFW	D_NUM
	BTFSS	STATUS,Z
	GOTO	LPM1


	CASENOT	ISECOND,D'1'
	GOTO	LPM1

	CASENOT	IHSECOND,D'1'
	GOTO	LPM1

	CASENOT	ITSECOND,D'1'
	GOTO	LPM1

	; MAKE THE LED DIMMER
	MOVLW	20
	SUBWF	FRAME_COUNT,W
	BTFSC	STATUS,C
	GOTO	LPM1

	; light the standby LED
	IF SYSTEM_VERSION == PROTOTYPE_1
		MOVLF 	PORTC,B'11111110'
	ELSE
		MOVLF 	PORTC,B'10111111'
	ENDIF

	CLRF	PORTA
	GOTO	MPX_I6

LPM1
	; EXTINGUISH DISPLAY
	MOVLF 	PORTC,0X7F
	MOVLF 	PORTA,B'00010000'

	GOTO	MPX_I6

